import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.modmuss50.mod-publish-plugin" version "0.6.3"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }

    runs {
        create("datagen") {
            name = "Minecraft Data"
            data()
            programArgs "--all", "--mod", "trading_floor"
            programArgs "--output", project(":common").file("src/generated/resources").absolutePath
            programArgs "--existing", file("src/main/resources").absolutePath

            environmentVariable("DATAGEN", "TRUE")
            //
//            // example of overriding the workingDirectory set in configureEach above
//            workingDirectory project.file('run-data')
//
//            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
//            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    // mavens for Forge-exclusives
    maven { url = "https://maven.theillusivec4.top/" } // Curios
    maven { // Create Forge and Registrate Forge
        url = "https://maven.tterrag.com/"
        content {
            includeGroup("com.tterrag.registrate")
            includeGroup("com.simibubi.create")
        }
    }
}

dependencies {
    forge("net.minecraftforge:forge:${minecraft_version}-${forge_version}")
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Create and its dependencies
    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_forge_version}:slim") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:${registrate_forge_version}")
    modImplementation("com.jozufozu.flywheel:flywheel-forge-${flywheel_forge_minecraft_version}:${flywheel_forge_version}")

    // Development QOL
    modLocalRuntime("mezz.jei:jei-${minecraft_version}-forge:${jei_version}") { transitive = false }

    // if you would like to add integration with JEI, uncomment this line.
//    modCompileOnly("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
}

processResources {
    // set up properties for filling into metadata
    Map<String, String> properties = Map.of(
            "version", version,
            "forge_version", forge_version.split("\\.")[0], // only specify major version of forge
            "minecraft_version", minecraft_version,
            "create_version", create_forge_version.split("-")[0] // cut off build number
    )
    properties.forEach((k, v) -> inputs.property(k, v))

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

loom {
    forge {
        mixinConfig(
                "trading_floor-common.mixins.json",
                "trading_floor.mixins.json"
        )
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

publishMods {
    def TOKENS = new HashMap<String, String>();
    file("$projectDir/.env").readLines().each() {
        def (key, value) = it.tokenize('=')
        TOKENS.put(key, value)
    }

    def cfg = new Yaml().load(new File("$projectDir/changelog.yaml").newInputStream())
    def changelogText = cfg[minecraft_version + "-" + mod_version];
    println("Found Changelog: \n" + changelogText)

    def forgeName = "Create: Trading Floor $mod_version Forge $minecraft_version";
//    def fabricName = "Create: Trading Floor $mod_version Fabric $minecraft_version";

    def forgeFile = project(":forge").tasks.named("remapJar").get().archiveFile;
//    def fabricFile = project(":fabric").tasks.named("remapJar").get().archiveFile;

    def forgeEmoji = "<:forge:1267784983771222088>";
//    def fabricEmoji = "<:fabric:1267785013936783421>";
//    def newVersionPing = "<@&1267779725040947243>";

    changelog = "Changelog: \n" + changelogText
    type = STABLE

    curseforge("forgeCF") {
        file = forgeFile
        displayName = forgeName
        projectId = "1102591"
        projectSlug = "create-trading-floor" // Required for discord webhook
        accessToken = TOKENS.get("CURSEFORGE_PUBLISH_TOKEN")
        minecraftVersions.add(minecraft_version)
        announcementTitle = "Curseforge download for $forgeEmoji"
        modLoaders.add("forge")

        javaVersions.add(JavaVersion.VERSION_17)
        clientRequired = true
        serverRequired = true
        requires("create")
    }
    modrinth("forgeMR") {
        file = forgeFile
        displayName = forgeName
        projectId = "WROfLLvn"
        accessToken = TOKENS.get("MODRINTH_PUBLISH_TOKEN")
        minecraftVersions.add(minecraft_version)
        announcementTitle = "Modrinth download for $forgeEmoji"
        modLoaders.add("forge")
        requires("create")
    }

//    curseforge("fabricCF") {
//        file = fabricFile
//        displayName = fabricName
//        projectId = "1072349"
//        projectSlug = "create-pattern-schematics" // Required for discord webhook
//        accessToken = TOKENS.get("CURSEFORGE_PUBLISH_TOKEN")
//        minecraftVersions.add(minecraft_version)
//        announcementTitle = "Curseforge download for $fabricEmoji"
//        modLoaders.add("fabric")
//
//        javaVersions.add(JavaVersion.VERSION_17)
//        clientRequired = true
//        serverRequired = true
//        requires("create-fabric", "fabric-api")
//    }
//    modrinth("fabricMR") {
//        file = fabricFile
//        displayName = fabricName
//        projectId = "cpqKG67r"
//        accessToken = TOKENS.get("MODRINTH_PUBLISH_TOKEN")
//        minecraftVersions.add(minecraft_version)
//        announcementTitle = "Modrinth download for $fabricEmoji"
//        modLoaders.add("fabric")
//        requires("create-fabric", "fabric-api")
//    }

    //TODO: uncomment after release
//    discord("discordUniversal") {
//        webhookUrl = TOKENS.get("DISCORD_WEBHOOK")
//        username = "New Version Announcement | Create: Trading Floor"
//        content = "$newVersionPing\n# Create: Trading Floor has been updated to $mod_version for $minecraft_version ($fabricEmoji / $forgeEmoji)\n " + changelogText
//    }
}
